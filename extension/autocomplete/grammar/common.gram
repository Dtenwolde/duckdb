Statement <-
	CreateStatement /
	SelectStatement /
	SetStatement /
	PragmaStatement /
	CallStatement /
	InsertStatement /
	DropStatement /
	CopyStatement /
	ExplainStatement /
	UpdateStatement /
	PrepareStatement /
	ExecuteStatement /
	AlterStatement /
	TransactionStatement /
	DeleteStatement /
	AttachStatement /
	UseStatement /
	DetachStatement /
	CheckpointStatement /
	VacuumStatement /
	ResetStatement /
	ExportStatement /
	ImportStatement /
	CommentStatement /
	DeallocateStatement /
	TruncateStatement /
	LoadStatement /
	InstallStatement

CatalogName <- ColIdOrString
SchemaName <- ColIdOrString
TableName <- ColIdOrString
ColumnName <- DottedIdentifier
IndexName <- ColId
SettingName <- ColId
PragmaName <- ColId
TableFunctionName <- ColIdOrString / FuncName
ConstraintName <- ColIdOrString
SequenceName <- Identifier
CollationName <- Identifier
CopyOptionName <- ColLabel
SecretName <- ColId

NumberLiteral <- < [+-]?[0-9]*([.][0-9]*)? >
StringLiteral <- '\'' [^\']* '\''

Type <- (TimeType / RowType / MapType / UnionType / NumericType / SimpleType) ArrayBounds*
SimpleType <- (QualifiedTypeName / CharacterType) TypeModifiers?
CharacterType <- ('CHARACTER'i 'VARYING'i?) /
                 ('CHAR'i 'VARYING'i?) /
                 ('NATIONAL'i 'CHARACTER'i 'VARYING'i?) /
                 ('NATIONAL'i 'CHAR'i 'VARYING'i?) /
                 ('NCHAR'i 'VARYING'i?) /
                 'VARCHAR'i
NumericType <- 'INT'i /
               'INTEGER'i /
               'SMALLINT'i /
               'BIGINT'i /
               'REAL'i /
               'FLOAT'i Parens(NumberLiteral)? /
               'DOUBLE'i 'PRECISION'i /
               'DECIMAL'i TypeModifiers? /
               'DEC'i TypeModifiers? /
               'NUMERIC'i TypeModifiers? /
               'BOOLEAN'i
QualifiedTypeName <- CatalogQualification? SchemaQualification? TypeName
TypeModifiers <- Parens(List(Expression))
RowType <- RowOrStruct Parens(List(ColIdType))
UnionType <- 'UNION'i Parens(List(ColIdType))
MapType <- 'MAP'i Parens(List(Type))
ColIdType <- ColId Type
ArrayBounds <- ('[' NumberLiteral? ']') / 'ARRAY'
TimeType <- TimeOrTimestamp TypeModifiers? TimeZone?
TimeOrTimestamp <- 'TIME'i / 'TIMESTAMP'i
TimeZone <- WithOrWithout 'TIME'i 'ZONE'i
WithOrWithout <- 'WITH'i / 'WITHOUT'i

RowOrStruct <- 'ROW'i / 'STRUCT'i

# internal definitions
%whitespace <- [ \t\n\r]*
List(D) <- D (',' D)* ','?
Parens(D) <- '(' D ')'
